/* See COPYRIGHT for copyright information. */

#include <inc/mmu.h>
#include <inc/memlayout.h>
#include <inc/trap.h>

#include <kern/picirq.h>


###################################################################
# exceptions/interrupts
###################################################################

/* TRAPHANDLER defines a globally-visible function for handling a trap.
 * It pushes a trap number onto the stack, then jumps to _alltraps.
 * Use TRAPHANDLER for traps where the CPU automatically pushes an error code.
 *
 * You shouldn't call a TRAPHANDLER function from C, but you may
 * need to _declare_ one in C (for instance, to get a function pointer
 * during IDT setup).  You can declare the function with
 *   void NAME();
 * where NAME is the argument passed to TRAPHANDLER.
 */
#define TRAPHANDLER(name, num)						\
	.globl name;		/* define global symbol for 'name' */	\
	.type name, @function;	/* symbol type is function */		\
	.align 2;		/* align function definition */		\
	name:			/* function starts here */		\
	pushl $(num);							\
	jmp _alltraps

/* Use TRAPHANDLER_NOEC for traps where the CPU doesn't push an error code.
 * It pushes a 0 in place of the error code, so the trap frame has the same
 * format in either case.
 */
#define TRAPHANDLER_NOEC(name, num)					\
	.globl name;							\
	.type name, @function;						\
	.align 2;							\
	name:								\
	pushl $0;							\
	pushl $(num);							\
	jmp _alltraps

.text

/*
 * Lab 3: Your code here for generating entry points for the different traps.
 */
TRAPHANDLER_NOEC(th0, T_DIVIDE)
TRAPHANDLER_NOEC(th1, T_DEBUG)
TRAPHANDLER_NOEC(th2, T_NMI)
TRAPHANDLER_NOEC(th3, T_BRKPT)
TRAPHANDLER_NOEC(th4, T_OFLOW)
TRAPHANDLER_NOEC(th5, T_BOUND)
TRAPHANDLER_NOEC(th6, T_ILLOP)
TRAPHANDLER_NOEC(th7, T_DEVICE)
TRAPHANDLER(th8, T_DBLFLT)
TRAPHANDLER_NOEC(th9, 9)  // reserved
TRAPHANDLER(th10, T_TSS)
TRAPHANDLER(th11, T_SEGNP)
TRAPHANDLER(th12, T_STACK)
TRAPHANDLER(th13, T_GPFLT)
TRAPHANDLER(th14, T_PGFLT)
TRAPHANDLER_NOEC(th15, 15)  // reserved
TRAPHANDLER_NOEC(th16, T_FPERR)
TRAPHANDLER(th17, T_ALIGN)
TRAPHANDLER_NOEC(th18, T_MCHK)
TRAPHANDLER_NOEC(th19, T_SIMDERR)

TRAPHANDLER_NOEC(th32, IRQ_OFFSET+IRQ_TIMER)  // timer: 32~47
TRAPHANDLER_NOEC(th33, IRQ_OFFSET+IRQ_TIMER+1)
TRAPHANDLER_NOEC(th34, IRQ_OFFSET+IRQ_TIMER+2)
TRAPHANDLER_NOEC(th35, IRQ_OFFSET+IRQ_TIMER+3)
TRAPHANDLER_NOEC(th36, IRQ_OFFSET+IRQ_TIMER+4)
TRAPHANDLER_NOEC(th37, IRQ_OFFSET+IRQ_TIMER+5)
TRAPHANDLER_NOEC(th38, IRQ_OFFSET+IRQ_TIMER+6)
TRAPHANDLER_NOEC(th39, IRQ_OFFSET+IRQ_TIMER+7)
TRAPHANDLER_NOEC(th40, IRQ_OFFSET+IRQ_TIMER+8)
TRAPHANDLER_NOEC(th41, IRQ_OFFSET+IRQ_TIMER+9)
TRAPHANDLER_NOEC(th42, IRQ_OFFSET+IRQ_TIMER+10)
TRAPHANDLER_NOEC(th43, IRQ_OFFSET+IRQ_TIMER+11)
TRAPHANDLER_NOEC(th44, IRQ_OFFSET+IRQ_TIMER+12)
TRAPHANDLER_NOEC(th45, IRQ_OFFSET+IRQ_TIMER+13)
TRAPHANDLER_NOEC(th46, IRQ_OFFSET+IRQ_TIMER+14)
TRAPHANDLER_NOEC(th47, IRQ_OFFSET+IRQ_TIMER+15)

TRAPHANDLER_NOEC(th48, T_SYSCALL)

/*
 * Lab 3: Your code here for _alltraps
 */
_alltraps:
	pushl %ds
	pushl %es
	pushal
	pushl $GD_KD
	popl %ds
	pushl $GD_KD
	popl %es
	pushl %esp
	call trap

